buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.springsource.org/libs-snapshot" }
        maven { url 'http://maven.tmatesoft.com/content/repositories/snapshots/' } // release plugin
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:0.5.0.M2'
        classpath 'com.eriwen:gradle-js-plugin:1.4.0'
        classpath 'au.com.ish.gradle:release:2.2b1' // release plugin
    }
}

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'release'

    group = 'com.yesnault.sag'

    release {
        failOnSnapshotDependencies = true
        allowLocalModifications = false
        releaseDryRun = true
        scm = 'git'
        //scm = 'svn'
        //username = 'fred'
        //password = 'secret01'
    }
    version = release.projectVersion
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}


subprojects {
    repositories {
        mavenCentral()
    }
    dependencies {
        testCompile "junit:junit:4.11"
    }
}

project(':services') {
    apply plugin: 'spring-boot'
    apply plugin: 'jetty'
    apply plugin: 'js'
    apply plugin: 'project-report'
    apply plugin: 'war'

    jettyRun {
        reload = "automatic"
        scanIntervalSeconds = 2
    }

    war {
        baseName = 'sag-services'
    }

    jar {
        baseName = 'sag-services'
    }

    dependencies {
        compile project(':business')
        compile project(':persistence')
        compile("org.springframework.boot:spring-boot-starter-web:0.5.0.M2")
        compile 'org.springframework:spring-web:3.1.2.RELEASE'
        compile 'org.springframework:spring-webmvc:3.1.2.RELEASE'

    }

    repositories {
        maven {
            url 'http://repo.springsource.org/libs-milestone'
        }
    }

    javascript.source {
        app {
            js {
                srcDir "${projectDir}/src/main/webapp/js/app/"
                include "**/*.js"
            }
        }
    }

    task jsApp(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
        source = javascript.source.app.js.files
        dest = file("${projectDir}/src/main/webapp/js/build/all-app.js")
    }

    task jsLibs(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
        source = ["${projectDir}/src/main/webapp/js/libs/jquery-1.10.2.min.js",
                "${projectDir}/src/main/webapp/js/libs/bootstrap.min.js",
                "${projectDir}/src/main/webapp/js/libs/angular.min.js",
                "${projectDir}/src/main/webapp/js/libs/angular-resource.min.js",
                "${projectDir}/src/main/webapp/js/libs/angular-route.min.js"]
        dest = file("${projectDir}/src/main/webapp/js/build/all-libs.js")
    }

    task minifyjs(type: com.eriwen.gradle.js.tasks.MinifyJsTask) {
        source = jsApp
        dest = file("${projectDir}/src/main/webapp/js/build/all-app.min.js")
    }

    classes {
        doFirst {
            jsApp.execute()
            jsLibs.execute()
        }
    }
}




project(':persistence') {
    dependencies {
        compile 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'
        compile 'org.springframework:spring-core:3.1.2.RELEASE'

        compile("org.springframework:spring-orm:4.0.0.M3")
        compile("org.springframework.data:spring-data-jpa:1.3.2.RELEASE")
        compile("org.hibernate:hibernate-entitymanager:4.2.1.Final")
        compile("com.h2database:h2:1.3.172")
        testCompile("junit:junit:4.11")
    }
    repositories {
        mavenCentral()
        maven { url "http://repo.springsource.org/libs-snapshot" }
        maven { url "https://repository.jboss.org/nexus/content/repositories/releases" }
    }

}
